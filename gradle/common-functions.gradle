def getRevision() {

    def revisionName = System.properties['revisionName']
    if(revisionName == null) {
        def revisionCmd = "git rev-parse HEAD"
        def revisionRes = revisionCmd.execute()
        revisionName = revisionRes.text.trim()
    }
    return revisionName
}

def getBranch() {
    def branchName = System.properties['branchName']
    if(branchName == null) {
        def branchCmd = "git rev-parse --abbrev-ref HEAD"
        def branchRes = branchCmd.execute()
        branchName = branchRes.text.trim()
    }
    return branchName
}

def determineVersion() {
    def versionNumber = "$version"
    def isSnapshot = true
    if (project.hasProperty('releaseType') && project.releaseType == "release") {
        isSnapshot = false
    }
    def finalVersionNumber = isSnapshot ? "$versionNumber-SNAPSHOT" : "$versionNumber-RELEASE"
    System.setProperty("calculatedVersion", finalVersionNumber)
    logger.lifecycle("Calculated version: " + finalVersionNumber)
    return finalVersionNumber
}

ext {
    determineVersion = this.&determineVersion
    getRevision = this.&getRevision    
    getBranch = this.&getBranch
}
